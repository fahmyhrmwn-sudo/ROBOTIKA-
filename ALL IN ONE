#include <NewPing.h>
#include <SoftwareSerial.h>

// --- Konfigurasi Ultrasonic ---
#define MAX_DISTANCE 200
#define TRIG_LEFT A0
#define TRIG_RIGHT A4
#define TRIG_FRONT A2

// Definisi Pin Echo
#define ECHO_FRONT A3
#define ECHO_LEFT A1 
#define ECHO_RIGHT  A5

NewPing sonarFront(TRIG_FRONT, ECHO_FRONT, MAX_DISTANCE);
NewPing sonarLeft(TRIG_LEFT, ECHO_LEFT, MAX_DISTANCE);
NewPing sonarRight(TRIG_RIGHT, ECHO_RIGHT, MAX_DISTANCE);

int distFront, distLeft, distRight;

// --- Konfigurasi IR (Line Follower) 
#define irRight 9
#define irLeft 8

// --- Konfigurasi Motor L298N ---
#define in1L298N 3
#define in2L298N 4
#define in3L298N 2
#define in4L298N 7
// Pin PWM (Kecepatan)
#define speed1L298N 5
#define speed2L298N 6

// --- LED & Button ---
#define led1  19  // HANYA 1 LED
#define button 12   // Tombol ganti mode

// --- Bluetooth HC-05 ---
#define unoRX 10
#define unoTX 11
SoftwareSerial HC05(unoRX, unoTX);

// --- Variabel Global ---
int rightStatus, leftStatus;
byte buttonValue = 0;
byte prevButtonValue = 0;
byte mode = 0; // 0: Idle, 1: WF, 2: LF, 3: BT

int speedNormal = 80; 
// Jika ingin kecepatan berbeda saat belok, ubah ini:
int speedTurn = 80;   
String voice;

// Variabel untuk Blinking LED
unsigned long previousMillis = 0;
boolean ledState = LOW;
long blinkInterval = 0; 

void setup() {
  Serial.begin(9600);
  HC05.begin(9600);

  pinMode(irRight, INPUT);
  pinMode(irLeft, INPUT);
  pinMode(button, INPUT_PULLUP);

  pinMode(in1L298N, OUTPUT);
  pinMode(in2L298N, OUTPUT);
  pinMode(in3L298N, OUTPUT);
  pinMode(in4L298N, OUTPUT);
  pinMode(speed1L298N, OUTPUT);
  pinMode(speed2L298N, OUTPUT);

  pinMode(led1, OUTPUT);

  // Set kecepatan awal default
  analogWrite(speed1L298N, speedNormal);
  analogWrite(speed2L298N, speedNormal);
}

void loop() {
  // Cek Tombol Mode
  buttonValue = digitalRead(button);
  if ((buttonValue == LOW) && (prevButtonValue == 0)) {
    mode++;
    prevButtonValue = 1;
    motorControl(LOW, LOW, LOW, LOW); 
    delay(200); 
  } else if (buttonValue == HIGH) {
    prevButtonValue = 0;
  }
  
  if (mode > 3) { 
    mode = 0;
  }

  // Atur Indikator 1 LED 
  handleSingleLed(mode);

  // Eksekusi Mode 
  switch (mode) {
    case 0: // Idle
      motorControl(LOW, LOW, LOW, LOW);
       Serial.println("Mode Nyantai");
      break;

    case 1: 
      WFMode();
      Serial.println("Mode Tembok");
      break;

    case 2: 
      LFMode();
       Serial.println("Mode Garis");
      break;

    case 3: 
      BTMode();
       Serial.println("Mode Bluetooth");
      break;
  }
}

// Fungsi Indikator 1 LED 
void handleSingleLed(int currentMode) {
  unsigned long currentMillis = millis();

  if (currentMode == 0) {
    digitalWrite(led1, LOW);
    ledState = LOW; 
  }
  else if (currentMode == 3) { // BT
    digitalWrite(led1, HIGH);
    ledState = HIGH;
  }
  else {
    if (currentMode == 1) blinkInterval = 800; // WF lambat
    else if (currentMode == 2) blinkInterval = 150; // LF cepat

    if (currentMillis - previousMillis >= blinkInterval) {
      previousMillis = currentMillis;
      ledState = !ledState;
      digitalWrite(led1, ledState);
    }
  }
}


void motorControl (int in1Value, int in2Value, int in3Value, int in4Value) {
  digitalWrite(in1L298N, in1Value);
  digitalWrite(in2L298N, in2Value);
  digitalWrite(in3L298N, in3Value);
  digitalWrite(in4L298N, in4Value);
  analogWrite(speed1L298N, speedNormal); 
  analogWrite(speed2L298N, speedNormal);
}

// --- WF Mode 
void WFMode() {
  distFront = sonarFront.ping_cm(); delay(10);
  distLeft  = sonarLeft.ping_cm();  delay(10);
  distRight = sonarRight.ping_cm(); delay(10);
  
  if (distFront == 0) distFront = 200;
  if (distLeft == 0) distLeft = 200;
  if (distRight == 0) distRight = 200;

  if (distFront < 5) {
    motorControl(LOW, HIGH, LOW, HIGH); // mundur
    delay(200);
  } 
  else if (distFront < 10 && distLeft > 10  && distRight > 15) {
    motorControl(HIGH, LOW, LOW,HIGH); //kanan
    delay(90);
  } 
   else if (distFront < 10 && distLeft > 10 ) {
    motorControl(LOW, HIGH, HIGH, LOW); //kiri
    delay(100);
  } 
  
  else if (distLeft < 6) { 
    motorControl(HIGH, LOW, LOW, LOW); // Menjauh
  }
  else if (distLeft > 16) { 
    motorControl(LOW, LOW, HIGH, LOW); // Mendekat
  }
  else {
    motorControl(HIGH, LOW, HIGH, LOW); // Lurus
  }
}

// --- LF Mode 
void LFMode() {
  rightStatus = digitalRead(irRight);
  leftStatus = digitalRead(irLeft);

  if (leftStatus == 0 && rightStatus == 0) {
    motorControl(HIGH, LOW, HIGH, LOW);
  } else if (leftStatus == 0 && rightStatus == 1) {
    motorControl(LOW, HIGH, HIGH, LOW);
  } else if (leftStatus == 1 && rightStatus == 0) {
    motorControl(HIGH, LOW, LOW, HIGH);
  } else if (leftStatus == 1 && rightStatus == 1) {
    motorControl(LOW, LOW, LOW, LOW);
  }
}

// --- BT Mode ---
void BTMode() {
  // Metode 1: String Voice (Aplikasi Voice Control)
 char c;
  while (HC05.available()) {
    delay(10);
   c = HC05.read();
    voice += c;
  }

  if (voice.length() > 0) {
    Serial.println(voice); 
    // logic mode bt
    if (voice == "forward"|| voice == "A"){ 
    motorControl(HIGH, LOW, HIGH, LOW);
    delay(1000);
    Serial.println("maju");
    }
    else if (voice == "backwards"|| voice == "C") {
    motorControl(LOW, HIGH, LOW, HIGH);
    delay(1000);
    Serial.println("mundur");
    }
    else if (voice == "right"|| voice =="D") {
    motorControl(LOW, HIGH, HIGH, LOW);
    delay(1000);
    Serial.println("kanan");
    }
    else if (voice == "left" || voice =="B") {
    motorControl(HIGH, LOW, LOW, HIGH);
    delay(1000);
    Serial.println("kiri");
   }
    else if (voice == "stop" || voice == "3"){
    motorControl(LOW, LOW, LOW, LOW);
    delay(1000);
    Serial.println("stop");
    

   }
      
    
    voice = ""; 
  }
  
}
